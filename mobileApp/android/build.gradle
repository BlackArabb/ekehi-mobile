// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext {
        buildToolsVersion = findProperty("android.buildToolsVersion") ?: "34.0.0"
        minSdkVersion = Integer.parseInt(findProperty("android.minSdkVersion") ?: "23")
        compileSdkVersion = Integer.parseInt(findProperty("android.compileSdkVersion") ?: "34")
        targetSdkVersion = Integer.parseInt(findProperty("android.targetSdkVersion") ?: "34")
        kotlinVersion = findProperty("android.kotlinVersion") ?: "1.9.23"
        ndkVersion = "26.1.10909125"
    }
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath("com.android.tools.build:gradle")
        classpath("com.facebook.react:react-native-gradle-plugin")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin")
    }
}

// Apply the new RN root plugin (manages most Gradle setup automatically)
apply plugin: "com.facebook.react.rootproject"

// Compatibility fixes for Gradle 8+
allprojects {
    configurations.all {
        resolutionStrategy {
            eachDependency { details ->
                if (details.requested.group == "org.jetbrains.kotlin") {
                    details.useVersion "1.9.23"
                }
            }
        }
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).configureEach {
        kotlinOptions {
            jvmTarget = "17"
        }
    }
}

// Fix for Gradle 8.0+ compatibility issues with expo-ads-admob (Jar duplication)
gradle.projectsEvaluated {
    tasks.withType(Jar).configureEach {
        duplicatesStrategy = DuplicatesStrategy.INCLUDE
    }
}

// Ensure Java 11 compatibility for Android subprojects
//Changed to Java 17
subprojects {
    plugins.withId("com.android.application") {
        android {
            compileOptions {
                sourceCompatibility JavaVersion.VERSION_17
                targetCompatibility JavaVersion.VERSION_17
            }
        }
    }
    plugins.withId("com.android.library") {
        android {
            compileOptions {
                sourceCompatibility JavaVersion.VERSION_17
                targetCompatibility JavaVersion.VERSION_17
            }
        }
    }
}

